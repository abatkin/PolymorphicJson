apply plugin: 'java'
apply plugin: 'maven-publish'

dependencies {
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1'
}

task listrepos {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
    }
}

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from { generatePomFileForMavenJavaPublication }
        rename ".*", "pom.xml"
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                scm {
                    connection = 'scm:git:https://github.com/abatkin/PolymorphicJson.git'
                    developerConnection = 'scm:git:https://github.com/abatkin/PolymorphicJson.git'
                    url = 'https://github.com/abatkin/PolymorphicJson'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "http://localhost:8081/artifactory/libs-release-local"
            def snapshotsRepoUrl = "http://localhost:8081/artifactory/libs-snapshot-local"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = artifactoryUser
                password = artifactoryPassword
            }
        }
    }
}

